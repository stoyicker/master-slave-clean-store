apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'kotlin-android-extensions'
// TODO Change to org.jetbrains.dokka-android
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    signingConfigs {
        release {
            storeFile new File("alias.keystore")
            storePassword("keystorePwd")
            keyAlias("alias")
            keyPassword("keyPwd")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug" // Do not change as Firebase depends on this
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            shrinkResources true
        }
    }
    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner rootProject.ext.androidTestInstrumentationRunner
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
}
repositories {
    jcenter()
}
dependencies {
    compile project(':data')
    compile project(':domain')
    compile project(':util-android')
    androidTestCompile project(':util-android-test')
    rootProject.ext.androidTestCompileAppDependencies.forEach {
        androidTestCompile(it) {
            exclude group: 'com.google.code.findbugs'
            exclude module: 'support-annotations'
        }
    }
    kapt rootProject.ext.annotationProcessorAppDependencies
    kaptAndroidTest rootProject.ext.annotationProcessorAndroidTestAppDependencies
    compile rootProject.ext.compileDependencies
    compile rootProject.ext.compileAppDependencies
    testCompile rootProject.ext.testCompileDependencies
}

apply plugin: 'com.google.gms.google-services'

setProperty("FirebaseServiceAccountFilePath", rootProject.rootDir.absolutePath + "/" +
        rootProject.ext.firebaseServiceAccountFileName)
