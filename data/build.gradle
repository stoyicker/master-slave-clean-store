apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    compileOptions {
        incremental false
    }

    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner rootProject.ext.androidTestInstrumentationRunner
        buildConfigField "String", "API_URL", System.getenv("API_URL") != null ? System.getenv("API_URL") : prop_api_url
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "8g"
    }

    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
}

repositories {
    jcenter()
}

dependencies {
    compile project(':domain')
    compile project(':util')
    compile rootProject.ext.compileDependencies
    provided rootProject.ext.providedDataDependencies
    rootProject.ext.compileDataDependencies.forEach {
        compile(it) {
            // RxJava is brought by the domain module
            exclude group: 'io.reactivex'
        }
    }
    testCompile rootProject.ext.testCompileDependencies
}
