import java.nio.file.Paths

apply from: 'buildsystem/dependencies.gradle'

buildscript {
	repositories {
        jcenter()
    }
    final def androidPluginVersion = "2.2.3"
    final def kotlinPluginVersion = rootProject.ext.kotlinVersion = "1.1.0"
    final def dokkaPluginVersion = "0.9.13"
    final def classpathDependencies = [
        "com.android.tools.build:gradle:$androidPluginVersion",
        "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinPluginVersion",
        "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaPluginVersion"
    ]
    dependencies {
        classpath classpathDependencies
    }
}

final def inducedVersion = System.getenv("ARTIFACT_VERSION")
final def staticAnalysisReportFolderTarget = project.rootDir.absolutePath + "/staticAnalysisReport"

rootProject.ext {
    androidCompileSdkVersion = 25
    androidMinSdkVersion = 9
    androidTargetSdkVersion = 25
    androidBuildToolsVersion = '25.0.2'
	androidVersionCode = inducedVersion != null ? Integer.parseInt(inducedVersion) : 0 // Depending on the git method locally causes IDE issues
	androidVersionName = String.valueOf(androidVersionCode)
	androidTestInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
	androidApplicationIdBase = "org.jorge.ms.%s"
	staticAnalysisReportTarget = staticAnalysisReportFolderTarget
    javaVersion = JavaVersion.VERSION_1_7
}

task clean(type: Delete, overwrite: true) {
    group "Verification"
    description "Deletes the build directories of the whole project and the static analysis result."
    rootProject.allprojects.each {
        delete it.buildDir
  	    delete Paths.get(it.projectDir.absolutePath, staticAnalysisReportFolderTarget).toAbsolutePath().toString()
  }
}
